#!/usr/bin/env zsh

# A single script that monitors each of the following for updates:
#     Homebrew (command line programs)
#     Homebrew's Casks (GUI apps)
#     Mac App Store
#     Python 3/2, Node.js, and Ruby environments
#
# Specifically does not monitor packages within script environments;
#   those might be pinned to specific versions and need to be checked
#   individually.

local -a envs=(brew cask mas python3 python2 node ruby)
local -a cmds=(check up)


help() {
    echo "Usage: envup [check|up] [environment]"
    echo "    defaults: envup check all"
}


all_check() {
    for env in "${envs[@]}"; do
        ${env}_check
    done
}

all_up() {
    echo "Update individually for now."
    exit 1
}


print_check() {
    if [[ $2 != $3 ]]; then
        echo "  ‚ö†Ô∏è   $1 latest version is $3; installed is $2."
    else
        echo "  ‚úÖ  $1 is up to date. ($2)"
    fi
}


brew_check() {
    /usr/local/bin/brew update > /dev/null 2>&1
    if [[ $? -ne 0 ]]; then
        echo "  ‚ùå  Couldn‚Äôt check Homebrew packages. Internet down?"
        return
    fi
    local outBrew=$(/usr/local/bin/brew outdated --quiet)
    local outFmt=$(echo $outBrew | xargs | sed -e "s/ /, /g")
    local outCount=$(echo $outBrew | wc -l | xargs)
    local info=""
    if [[ ${#outBrew} == 0 ]]; then
        info="  ‚úÖ  Homebrew packages are up to date."
    elif [[ $outCount == 1 ]]; then
        info="  ‚ö†Ô∏è   Homebrew has 1 outdated package. ($outFmt)"
    else
        info="  ‚ö†Ô∏è   Homebrew has $outCount outdated packages. ($outFmt)"
    fi
    echo ${info:0:$COLUMNS}
}

brew_up() {
    /usr/local/bin/brew upgrade
    /usr/local/bin/brew cleanup -s
    rm -rf $(/usr/local/bin/brew --cache)
    all_check # since this might have updated one of the envs
}


cask_check() {
    /usr/local/bin/brew update > /dev/null 2>&1
    if [[ $? -ne 0 ]]; then
        echo "  ‚ùå  Couldn‚Äôt check Homebrew casks. Internet down?"
        return
    fi
    local outCask=$(/usr/local/bin/brew cask outdated --quiet)
    local outFmt=$(echo $outCask | xargs | sed -e "s/ /, /g")
    local outCount=$(echo $outCask | wc -l | xargs)
    local info=""
    if [[ ${#outCask} == 0 ]]; then
        info="  ‚úÖ  Homebrew casks are up to date."
    elif [[ $outCount == 1 ]]; then
        info="  ‚ö†Ô∏è   Homebrew has 1 outdated cask. ($outFmt)"
    else
        info="  ‚ö†Ô∏è   Homebrew has $outCount outdated casks. ($outFmt)"
    fi
    echo ${info:0:$COLUMNS}
}

cask_up() {
    # echo "Cask might need sudo..."
    # sudo -v
    /usr/local/bin/brew cask upgrade
    /usr/local/bin/brew cleanup
    rm -rf $(/usr/local/bin/brew --cache)
    echo "  ‚úÖ  Homebrew casks are up to date."
}


mas_check() {
    # <sigh> no "--quiet" modifier for mas :-/
    local outMas=$(/usr/local/bin/mas outdated | sed -E 's/^[0-9]+[[:space:]]+(.*)[[:space:]]+\([0-9.]*[[:space:]]+->[[:space:]]+[0-9.]*\)$/\1/')
    local outFmt=$(echo $outMas | paste -düêº -s - | sed -e "s/üêº/, /g")
    local outCount=$(echo $outMas | wc -l | xargs)
    local info=""
    if [[ ${#outMas} == 0 ]]; then
        info="  ‚úÖ  Mac App Store apps are up to date."
    elif [[ $outCount == 1 ]]; then
        info="  ‚ö†Ô∏è   Mac App Store has 1 outdated app. ($outFmt)"
    else
        info="  ‚ö†Ô∏è   Mac App Store has $outCount outdated apps. ($outFmt)"
    fi
    echo ${info:0:$COLUMNS}
}

mas_up() {
    /usr/local/bin/mas upgrade
    echo "  ‚úÖ  Mac App Store apps are up to date."
}


env_remVer() {
    $1 install -l \
        | grep -vE "\s*[a-zA-Z-]" \
        | sort -V \
        | grep "^\s*$2" \
        | tail -1 \
        | xargs
}
env_locVer() {
    $1 global | grep "^$2"
}

env_check() {
    remoteVersion=$( env_remVer $1 $2 )
    localVersion=$( env_locVer $1 $2 )

    print_check $3 $localVersion $remoteVersion
}

env_migration_failed() {
    {
        echo
        echo "MIGRATION FAILED"
        echo
        echo "Inspect the requirements file: $REQ_FILE"
    } 1>&2
    exit
}


node_check() {
    /usr/local/bin/nodenv update-version-defs > /dev/null 2>&1
    env_check nodenv "" "Node.js"
    /usr/local/bin/nodenv prune-version-defs > /dev/null 2>&1
}

node_up() {
    newVersion=$( env_remVer nodenv "" )
    oldVersion=$( env_locVer nodenv "" )

    if [[ $newVersion == $oldVersion ]]; then
        print_check "Node.js" $newVersion $newVersion
        return
    fi

    nodenv install $newVersion

    trap env_migration_failed ERR
        local seed="$(date '+%Y%m%d%H%M%S').$$"
        REQ_FILE="$HOME/node-npmfile.$seed.txt"
        npm list --global --depth=0 \
            | tail -n +2 \
            | cut -d" " -f2 \
            | grep -v "npm@" \
            > "$REQ_FILE"
        nodenv global $newVersion
        npm install --global npm
        npm install --global $(cat "$REQ_FILE")

        nodenv uninstall -f $oldVersion
        nodenv rehash
        rm "$REQ_FILE"
        print_check "Node.js" $newVersion $newVersion
    trap - ERR
}

python3_check() {
    env_check pyenv 3 "Python 3"
}

python3_up() {
    newVersion=$( env_remVer pyenv 3 )
    oldVersion=$( env_locVer pyenv 3 )
    version2=$( env_locVer pyenv 2)

    if [[ $newVersion == $oldVersion ]]; then
        print_check "Python 3" $newVersion $newVersion
        return
    fi

    LDFLAGS="-L/usr/local/opt/zlib/lib -L/usr/local/opt/sqlite/lib" CPPFLAGS="-I/usr/local/opt/zlib/include -I/usr/local/opt/sqlite/include" pyenv install $newVersion
    pyenv global $newVersion $version2 miniconda3-latest
    pyenv rehash

    trap env_migration_failed ERR
        local seed="$(date '+%Y%m%d%H%M%S').$$"
        REQ_FILE="$HOME/python3.requirements.$seed.txt"
        ~/.pyenv/versions/$oldVersion/bin/pip freeze | grep -v "^-e" > "$REQ_FILE"
        ~/.pyenv/versions/$newVersion/bin/pip install --upgrade pip
        ~/.pyenv/versions/$newVersion/bin/pip install -r "$REQ_FILE"
        pyenv uninstall -f $oldVersion
        pyenv rehash
        rm "$REQ_FILE"
        print_check "Python 3" $newVersion $newVersion
    trap - ERR
}


python2_check() {
    env_check pyenv 2 "Python 2"
}

python2_up() {
    newVersion=$( env_remVer pyenv 2 )
    oldVersion=$( env_locVer pyenv 2 )
    version3=$( env_locVer pyenv 3)

    if [[ $newVersion == $oldVersion ]]; then
        print_check "Python 2" $newVersion $newVersion
        return
    fi

    LDFLAGS="-L/usr/local/opt/zlib/lib -L/usr/local/opt/sqlite/lib" CPPFLAGS="-I/usr/local/opt/zlib/include -I/usr/local/opt/sqlite/include" pyenv install $newVersion
    pyenv global $version3 $newVersion miniconda3-latest
    pyenv rehash

    trap env_migration_failed ERR
        local seed="$(date '+%Y%m%d%H%M%S').$$"
        REQ_FILE="$HOME/python2-requirements.$seed.txt"
        ~/.pyenv/versions/$oldVersion/bin/pip freeze | grep -v "^-e" > "$REQ_FILE"
        ~/.pyenv/versions/$newVersion/bin/pip install --upgrade pip
        ~/.pyenv/versions/$newVersion/bin/pip install -r "$REQ_FILE"
        pyenv uninstall -f $oldVersion
        pyenv rehash
        rm "$REQ_FILE"
        print_check "Python 2" $newVersion $newVersion
    trap - ERR
}


ruby_check() {
    env_check rbenv 2 "Ruby"
}

ruby_up() {
    newVersion=$( env_remVer rbenv 2 )
    oldVersion=$( env_locVer rbenv 2 )

    if [[ $newVersion == $oldVersion ]]; then
        print_check "Ruby" $newVersion $newVersion
        return
    fi

    rbenv install $newVersion
    rbenv global $newVersion
    newGem=~/.rbenv/versions/$newVersion/bin/gem
    oldGem=~/.rbenv/versions/$oldVersion/bin/gem

    trap env_migration_failed ERR
        local seed="$(date '+%Y%m%d%H%M%S').$$"
        REQ_FILE="$HOME/ruby-gemfile.$seed.txt"
        $oldGem list > "$REQ_FILE"
        $newGem update --system
        yes | $newGem update
        for i in $($oldGem list | awk '{ if ( $1 !~ /^$/ ) print $1}'); do
            if [[ $($newGem list "${i}") != *"${i}"* ]]; then
                $newGem install -f "${i}"
            fi
        done

        rbenv uninstall -f $oldVersion
        rbenv rehash
        rm "$REQ_FILE"
        print_check "Ruby" $newVersion $newVersion
    trap - ERR
}


[[ $ZSH_EVAL_CONTEXT =~ :file$ ]] && sourced=1 || sourced=0
if [[ $sourced -ne 1 ]]; then
    if [[ $#@ -lt 2 ]]; then
        val="all"
    fi
    if [[ $#@ -lt 1 ]]; then
        command="check"
    fi
    if [[ $#@ -ge 2 ]]; then
        command=$1
        val=$2
    fi

    if [[ ${envs[(ie)$val]} -gt ${#envs} ]]; then
        if [[ $val != "all" ]]; then
            echo "INVALID ENVIRONMENT: $val"
            help
            exit 1
        fi
    fi

    if [[ ${cmds[(ie)$command]} -gt ${#cmds} ]]; then
        echo "INVALID COMMAND: $command"
        help
        exit 1
    fi

    ${val}_${command}
fi

